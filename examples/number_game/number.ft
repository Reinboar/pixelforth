INCLUDE" gb-prelude.ft"
INCLUDE" text.ft"

: SCREEN-OFF
  WAIT-VBLANK
  00h
    LCD-DISABLE
  LCDC C!
;

: SCREEN-ON
  00h
    LCD-ENABLE
    LCD-WINDOW-DISABLE
    LCD-BG-TILE-PATTERN-0
    LCD-BG-TILE-MAP-0
    LCD-OBJ-DISABLE
    LCD-BG-WINDOW-ENABLE
  LCDC C!
;

SCREEN-OFF
LOAD-FONT
CLEAR-SCREEN

" Welcome to the" VARIABLE: GREET-TEXT-1 !
" Number Game!" VARIABLE: GREET-TEXT-2 !
" Press  Start" VARIABLE: START-TEXT !
" Higher or Lower?" VARIABLE: PROMPT-TEXT !
" HIGHER" VARIABLE: HIGH-TEXT !
" LOWER" VARIABLE: LOW-TEXT !

00h VARIABLE: CUR-SELECTION !
CONSTANT: HIGH-SELECTION 00h
CONSTANT: LOW-SELECTION 01h

00h VARIABLE: OLD-NUMBER !
00h VARIABLE: CURRENT-NUMBER !
00h VARIABLE: CORRECT-GUESSES !

: WAIT-FOR-BUTTON ( button -- )
  [ DUP READ-JOYPAD = NOT ] WHILE
  DROP
;

DEADh VARIABLE: RNG-A !
BEEFh VARIABLE: RNG-B !
FACEh VARIABLE: RNG-C !
01h VARIABLE: RNG-COUNT !

: RANDOMIZE 
  01h RNG-COUNT +!
  RNG-A @ RNG-C @ ^ RNG-COUNT @ ^ RNG-A !
  RNG-B @ RNG-A @ + RNG-B !
  RNG-C @ 01h RNG-B @ >> + RNG-A @ ^ RNG-C !
  RNG-C @ FFh &
;
  
: INIT-RNG 
  FF04h C@ RNG-A @ ^ RNG-A !
  FF04h C@ RNG-B @ ^ RNG-B !
  FF04h C@ RNG-C @ ^ RNG-C !
  RANDOMIZE DROP
; 

: NEXT-NUMBER 
  CURRENT-NUMBER @ OLD-NUMBER !
  RANDOMIZE 0Fh & CURRENT-NUMBER ! 
;

: DRAW-START-SCREEN
  SCREEN-ON

  WAIT-VBLANK
  03h 02h SET-CURSOR!
  GREET-TEXT-1 @ PRINT

  WAIT-VBLANK
  04h 03h SET-CURSOR!
  GREET-TEXT-2 @ PRINT

  WAIT-VBLANK
  04h 0Ah SET-CURSOR!
  START-TEXT @ PRINT
;

: DRAW-SELECTION
  WAIT-VBLANK
  02h 04h SET-CURSOR!
  PROMPT-TEXT @ PRINT
  
  WAIT-VBLANK
  07h 07h SET-CURSOR!
  HIGH-TEXT @ PRINT

  WAIT-VBLANK
  07h 08h SET-CURSOR!
  LOW-TEXT @ PRINT
;

: DRAW-SELECTION-CURSOR
  CUR-SELECTION @ HIGH-SELECTION =
    [ 06h 08h 06h 07h ]
    [ 06h 07h 06h 08h ]
  IF
  WAIT-VBLANK
  SET-CURSOR!
  0Dh PRINT-CHAR
  WAIT-VBLANK
  SET-CURSOR!
  00h PRINT-CHAR
;

: DRAW-CURRENT-NUMBER
  08h 05h SET-CURSOR!
  CURRENT-NUMBER @ NUM-TO-STR WAIT-VBLANK PRINT FFFBh ALLOT
;

: DRAW-CORRECT-GUESSES
  03h 17d SET-CURSOR!
  " CORRECT = " WAIT-VBLANK PRINT
  0Dh 17d SET-CURSOR!
  CORRECT-GUESSES @ NUM-TO-STR WAIT-VBLANK PRINT FFFBh ALLOT
;

: DRAW-GAME-OVER
  WAIT-VBLANK SCREEN-OFF
  CLEAR-SCREEN
  05h 04h SET-CURSOR!
  " GAME  OVER" PRINT
  SCREEN-ON
;

: CHECK-GUESS
  NEXT-NUMBER
  OLD-NUMBER @ CURRENT-NUMBER @ >
  CUR-SELECTION @ BREAK =
    [ 
      01h CORRECT-GUESSES +!
      DRAW-CORRECT-GUESSES
    ]
    [ DRAW-GAME-OVER DRAW-CURRENT-NUMBER DRAW-CORRECT-GUESSES PAUSE ]
  IF
;

: CHECK-INPUT
  READ-JOYPAD
  DUP JOYPAD-UP =   [ HIGH-SELECTION CUR-SELECTION ! ] WHEN
  DUP JOYPAD-DOWN = [ LOW-SELECTION CUR-SELECTION ! ] WHEN
  JOYPAD-A =        [ CHECK-GUESS ] WHEN
;

DRAW-START-SCREEN
JOYPAD-START WAIT-FOR-BUTTON
WAIT-VBLANK SCREEN-OFF
CLEAR-SCREEN
SCREEN-ON

INIT-RNG
NEXT-NUMBER

DRAW-CORRECT-GUESSES
DRAW-SELECTION

[ 
  CHECK-INPUT
  DRAW-CURRENT-NUMBER
  DRAW-SELECTION-CURSOR
  TRUE 
] WHILE
